import java.util.stream.Collectors

plugins {
  id "java"
  id "org.springframework.boot" version "1.5.8.RELEASE"
}

version "0.0.1"
group "daggerok"
sourceCompatibility = JavaVersion.VERSION_1_8

repositories { mavenCentral() }

dependencies {
  compile("org.springframework.boot:spring-boot-starter-data-elasticsearch")
  compile("org.springframework.boot:spring-boot-starter-hateoas")
  compile("org.springframework.boot:spring-boot-starter-web")
  compile("org.springframework.boot:spring-boot-starter-actuator")
  runtime("org.springframework.boot:spring-boot-devtools")
  compileOnly("org.projectlombok:lombok")
  testCompile("org.springframework.boot:spring-boot-starter-test")
}

springBoot { executable = true }

[test, build, bootRun].each { task ->
  def dependsOnTasks = [assemble, ":docker:composeUp"]
  task.dependsOn dependsOnTasks
  task.shouldRunAfter ([clean] + dependsOnTasks)
  task.finalizedBy ":docker:composeDown"
}

clean {
  ["build", "docker-cluster", "elasticsearch", "data", "out"].each { folder ->
    delete "$projectDir/$folder"
  }
}

[assemble, clean].each {
  it.dependsOn ":blog:$it.name"
}

task wrapper(type: Wrapper) {
  gradleVersion = "4.3"
  distributionType = "ALL"
}
